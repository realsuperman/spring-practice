plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.10'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.asciidoctor.jvm.convert" version '3.3.2'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	asciidoctorExt
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'com.opencsv:opencsv:5.5'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	runtimeOnly 'com.h2database:h2'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext { // 스니펫 생성 디렉토리 설정
	set('snippetsDir', file("build/generated-snippets"))
	snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor { // asciidoctor 테스크 성정
	configurations 'asciidoctorExt' // asciidoctor 확장 설정
	baseDirFollowsSourceFile()
	inputs.dir snippetsDir // 스니펫 위치 설정
	dependsOn test // test 테스크 후 asciidoctor 실행 설정
}

asciidoctor.doFirst {
	delete file('src/main/resources/static/docs')
}

task createDocument(type: Copy){
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder-jammy-base:latest'
}

bootJar {
	dependsOn createDocument // bootJar 테스크 실행 시, asciidoctor 테스크가 먼저 실행이 되도록
	from("${asciidoctor.outputDir}") { // 생성한 html 파일 위치
		into 'static/docs'
	}
}